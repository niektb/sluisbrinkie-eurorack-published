; by Niek ten Brinke
; techno_kick.spn

; A rumble effect for techno kicks

; Pots:
; pot0: amount of distortion
; pot1: volume of the smeared signal
; pot2: lowpass filter frequency for the smeared signal

equ temp REG0
equ gain REG1
equ lpf1p REG2
equ	apout REG3	;holding reg input AP signal
equ adc_in REG4

equ pkfil REG5
equ lim_gain REG6

equ lpf_out REG7

equ k_neg_two -2.0
equ k_lpf   0.69

mem	api1	122
mem	api2	303
mem	api3	553
mem	api4	922
	
mem	ap1	    3823
mem	del1	8500	;input = left output

mem	ap2 	4732
mem	del2	7234	;input = right output

equ	krt	    0.1	    ;adjust reverb time
equ	kap	    0.5	;adjust AP coefficients

ldax pot0  ; get pot0 value
sof -1.5, 0.0 ; bring to -2..0 range
wrax gain,0 ; store gain, reset ACC

ldax adcl
wrax adc_in,1 ; store adc input
wrax dacr,1 ;right (dry) output

sof 0.25,0.0 ; attenuate input
rda	api1#,kap	;do 4 APs
wrap	api1,-kap
rda	api2#,kap
wrap	api2,-kap
rda	api3#,kap
wrap	api3,-kap
rda	api4#,kap
wrap	api4,-kap
wrax	apout,1		;write to min, keep in ACC

;first loop apd:
			;AP'd input in ACC
rda	del2#,krt	;read del2, scale by Krt
rda	ap1#,-kap	;do loop ap
wrap	ap1,kap
wra	del1,1.0	;write delay, x2 for dac out

; reset ACC to zero
sof 0.0,0.0

;second loop apd:
rdax	apout,1		;get input signal again
rda	del1#,krt	;as above, to other side of loop
rda	ap2#,kap
wrap	ap2,-kap
wra	del2,0.5

; add dry signal to wet
rdax adc_in,0.5 ; get input signal again

mulx gain ; apply gain from pot0
mulx gain ; apply gain from pot0
mulx gain ; apply gain from pot0
sof k_neg_two,0.0 ; bring up gain by 12dB
sof k_neg_two,0.0
sof k_neg_two,0.0 ; bring up gain by 12dB

wrax temp,-0.33333 
mulx temp
mulx temp
rdax temp,1
sof 1.5,0.0

; LPF to smooth distortion
sof 0.04680607, 0.0
rdax lpf1p, -0.04680607
mulx pot2
rdax lpf1p, 1.0
wrax lpf1p,	1	

mulx pot1 ; volume control

rdax adc_in,0.5 ; Add undistorted signal

sof 0.25,0.0 ; reduce level before limiting

wrax lpf_out,1

; limiter section
maxx pkfil,0.99998	;compare with pkfil*.999 (abs)
wrax pkfil,1		;write peak value back
log	-1,-0.125	
exp	1,0		;1/x
wrax	lim_gain,1
mulx lpf_out
sof	-2,0            ;restore gain, to avoid output clipping
sof	-1.5,0            ;restore gain, to avoid output clipping

rdax adc_in,0.5 ; Add undistorted signal

; write output
wrax dacl,0